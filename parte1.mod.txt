/*A continuación se implementa toda la modelización de la parte 1. El fichero
con los datos (.data) se adjunta en un .txt por separado junto a este.*/

/*Creación de parámetros*/
set PARADAS;
set RECOGIDAS_COL within PARADAS;
set RECOGIDAS_PARK within PARADAS;
set RECOGIDAS within PARADAS;
set S1 within PARADAS;
set S2 within PARADAS;
set S3 within PARADAS;
set PARKING within PARADAS;
set COLEGIO within PARADAS;

param costes {i in PARADAS, j in PARADAS};
param personas_paradas {i in PARADAS};
param personas_total;
param capacidad_bus;

/*Variables de decisión*/
var destinos {i in PARADAS, j in PARADAS} binary;
var flujo {i in PARADAS, j in PARADAS} integer, >= 0;

/*Función objetivo*/
minimize Profit {k in PARKING} : sum{i in PARADAS, j in PARADAS} (costes[i, j]*destinos[i,j]*5) + (sum{i in PARADAS} destinos[k, i]*120);

/*Restricciones*/

#A cada parada llega 1 ruta.
s.t. Llegadas {j in RECOGIDAS} : sum{i in PARADAS} destinos[i, j] <= 1;

#De cada parada sale sólo 1 ruta.
s.t. Salidas {j in RECOGIDAS} : sum{i in PARADAS} destinos[j, i] <= 1;

#El número de rutas no supera el número de autobuses.
s.t. Autobuses {j in PARKING} : sum{i in PARADAS} destinos[j, i] <= 3;

#Todas las rutas salen del parking y llegan al colegio.
s.t. Rutas_totales {j in PARKING, k in COLEGIO} : sum{i in PARADAS} destinos[j, i] = sum{i in PARADAS} destinos[i, k];

#No se puede superar la capacidad del autobus al salir de una parada.
s.t. Capacidad {j in RECOGIDAS} : sum{i in PARADAS} flujo[i, j] <= capacidad_bus;

#Los alumnos que salen de una parada deben ser los que entran mas los que estaban esperando.
s.t. Flujo_S1 {j in RECOGIDAS} : sum{i in PARADAS} flujo[j, i] - (sum{i in PARADAS} flujo[i, j] + (sum{i in PARADAS} (destinos[j, i] * personas_paradas[i]))) = 0;

#Llegan 8 alumnos al colegio.
s.t. Alumnos_llegada {j in COLEGIO} : sum{i in PARADAS} flujo[i, j] = personas_total;

#Debe salir mínimo un autobus.
s.t. Min_autobus {j in PARKING} : sum{i in PARADAS} destinos[j, i] >= 1;

#No se pueden repetir paradas. (Se evitan los bucles).
s.t. Bucle {i in RECOGIDAS_COL, j in RECOGIDAS_PARK} : destinos[j, i] + destinos[i, j] <= 1;

#Ninguna parada es el punto origen de una ruta.
s.t. No_origen {j in RECOGIDAS} : sum{i in PARADAS} destinos[j, i] - sum{i in PARADAS} destinos[i, j] = 0;

#Del colegio no se llevan alumnos a otras paradas.
s.t. Colegio_no_salen {j in COLEGIO} : sum{i in PARADAS} flujo[j, i] = 0;

#Del aparcamiento a la parada solo se recogen a los alumnos que hay en esta.
s.t. Flujo_P_S1 {i in PARKING, j in RECOGIDAS} : flujo[i, j] = destinos[i, j] * personas_paradas[j];

#Control de flujo entre paradas. (Restringimos para que la matriz de flujo tenga valores coherentes).
s.t. Flujo {i in RECOGIDAS, j in RECOGIDAS_COL} : flujo[i, j] <= destinos[i, j] * 69;
s.t. Flujo_2 {i in RECOGIDAS, j in RECOGIDAS} : destinos[i, j] <= flujo[i, j];

#No se puede ir del parking al colegio directamente.
s.t. No_parking_colegio {i in PARKING, j in COLEGIO} : flujo[i, j] = 0;